buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'digital.wup:android-maven-publish:3.1.0'
    }
}
apply plugin: 'com.android.library'
apply plugin: 'digital.wup.android-maven-publish'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "0.1.0-SNAPSHOT"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError true
        htmlReport true
    }
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }
    compile('com.crashlytics.sdk.android:crashlytics:2.7.0@aar') {
        transitive = true
    }

    testCompile 'junit:junit:4.12'
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            groupId "com.github.daberni"
            artifactId "crashlytics-logback-appender"
            version android.defaultConfig.versionName

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST

                    name 'Crashlytics Logback Appender'
                    description 'Simple logback appender to log to Crashlytics for enhanced crash reports Edit'
                    licenses {
                        license {
                            name "The Apache Software License, Version 2.0"
                            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                            distribution 'repo'
                        }
                    }
                    scm {
                        url 'https://github.com/daberni/crashlytics-logback-apender.git'
                        connection 'scm:git@github.com:daberni/crashlytics-logback-apender.git'
                        developerConnection 'scm:git@github.com:me/my-repo.git'
                    }
                    developers {
                        developer {
                            id "daberni"
                            name "Bernhard Danecker"
                            email "bernhard.danecker@gmail.com"
                        }
                    }
                }
            }

            from components.android
        }
    }
    repositories {
        maven {
            credentials {
                username sonatypeUser
                password sonatypePassword
            }
            url android.defaultConfig.versionName.endsWith("-SNAPSHOT") ? "https://oss.sonatype.org/content/repositories/snapshots" : "https://oss.sonatype.org/service/local/staging/deploy/maven2"
        }
    }
}
