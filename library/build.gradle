buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "0.1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError true
        htmlReport true
    }
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }
    compile('com.crashlytics.sdk.android:crashlytics:2.7.0@aar') {
        transitive = true
    }

    testCompile 'junit:junit:4.12'
}

group "com.github.daberni"
version android.defaultConfig.versionName
project.archivesBaseName = 'crashlytics-logback-appender'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Crashlytics Logback Appender'
            description 'Simple logback appender to log to Crashlytics for enhanced crash reports Edit'
            url 'https://github.com/daberni/crashlytics-logback-apender'
            inceptionYear '2017'

            packaging 'aar'
            groupId "com.github.daberni"
            artifactId "crashlytics-logback-appender"

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/daberni/crashlytics-logback-apender'
                connection 'scm:git@github.com:daberni/crashlytics-logback-apender.git'
            }
            developers {
                developer {
                    id "daberni"
                    name "Bernhard Danecker"
                    email "bernhard.danecker@gmail.com"
                }
            }
        }
    }
}

bintray {
    user = project.property("bintray.user")
    key = project.property("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = 'oss'
        name = 'crashlytics-logback-appender'
        description 'Simple logback appender to log to Crashlytics for enhanced crash reports Edit'
        websiteUrl = 'https://github.com/daberni/crashlytics-logback-apender'
        vcsUrl = 'https://github.com/daberni/crashlytics-logback-apender.git'
        licenses = ['Apache-2.0']
        publish = true
        version {
            name = android.defaultConfig.versionName
            gpg {
                sign = true
                passphrase = project.property("bintray.gpg.password")
            }
        }
    }
}
